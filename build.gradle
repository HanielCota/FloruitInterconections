plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.2'
}
group = 'com.floruit'
version = '1.0-SNAPSHOT'
tasks.build.dependsOn shadowJar

repositories {
    mavenCentral()
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven { url = 'https://repo.aikar.co/content/groups/aikar/' }

    maven {
        url = "https://libraries.minecraft.net/"
    }
}

dependencies {
    compileOnly("net.md-5:bungeecord-api:1.21-R0.1-SNAPSHOT")

    // Commands framework
    compileOnly 'co.aikar:acf-bungee:0.5.0-SNAPSHOT'

    // Lombok for annotation processing
    annotationProcessor 'org.projectlombok:lombok:1.18.34'
    compileOnly 'org.projectlombok:lombok:1.18.34'
}


def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

shadowJar {
    configurations = [project.configurations.compileClasspath]
    relocate 'co.aikar.commands', 'com.floruit.floruitInterconections.acf'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    expand props
    filteringCharset 'UTF-8'
    filesMatching('bungee.yml') {
        expand props
    }
}
